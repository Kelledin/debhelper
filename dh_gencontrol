#!/usr/bin/perl -w

=head1 NAME

dh_gencontrol - generate and install control file

=cut

use strict;
use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

B<dh_gencontrol> [S<I<debhelper options>>] [S<B<--> I<params>>]

=head1 DESCRIPTION

B<dh_gencontrol> is a debhelper program that is responsible for generating
control files, and installing them into the I<DEBIAN> directory with the
proper permissions.

This program is merely a wrapper around L<dpkg-gencontrol(1)>, which calls
it once for each package being acted on, and passes in some additional
useful flags.

=head1 OPTIONS

=over 4

=item B<--> I<params>

Pass I<params> to L<dpkg-gencontrol(1)>.

=item B<-u>I<params>, B<--dpkg-gencontrol-params=>I<params>

This is another way to pass I<params> to L<dpkg-gencontrol(1)>.
It is deprecated; use B<--> instead.

=back

=cut

init(options => {
	"dpkg-gencontrol-params=s", => \$dh{U_PARAMS},
});


my $src_pkg;

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	my $ext=pkgext($package);
	my $ddeb_tmp = "debian/.debhelper/${package}/ddeb-root";

	my $substvars="debian/${ext}substvars";
	
	my $changelog=pkgfile($package,'changelog');
	if (! $changelog) {
		$changelog='debian/changelog';
	}

	if ( ! -d "$tmp/DEBIAN" ) {
		install_dir("$tmp/DEBIAN");
	}

	# avoid gratuitous warning
	if (! -e $substvars || system("grep -q '^misc:Depends=' $substvars") != 0) {
		complex_doit("echo misc:Depends= >> $substvars");
	}
	# avoid (another) gratuitous warning
	if (! -e $substvars || system("grep -q '^misc:Pre-Depends=' $substvars") != 0) {
		complex_doit("echo misc:Pre-Depends= >> $substvars");
	}
	

	if ( -d $ddeb_tmp) {
		my $arch = package_arch($package);
		my $ddeb_filename = ddeb_filename($package);
		$src_pkg = sourcepackage() if not defined($src_pkg);
		# Remove and override more or less every standard field.
		my @ddeb_options = (qw(
			-UPre-Depends -URecommends -USuggests -UEnhances -UProvides -UEssential
			-UReplaces -UBreaks -UConflicts
			-DPriority=extra -DSection=debug -DMulti-Arch=same
			),
			 "-DPackage=${package}-dbgsym",
			 "-DDepends=${package}:${arch} (= \${binary:Version})",
			 "-DDescription=Debug symbols for ${package}",
			 "-DSource=${src_pkg}",
			 "-n${ddeb_filename}",
		);
		if ( ! -d "${ddeb_tmp}/DEBIAN" ) {
			install_dir("${ddeb_tmp}/DEBIAN");
                }
		doit("dpkg-gencontrol", "-p${package}", "-l$changelog", "-T$substvars",
			"-P${ddeb_tmp}",@{$dh{U_PARAMS}}, @ddeb_options);

		doit("chmod","0644","${ddeb_tmp}/DEBIAN/control");
		doit("chown","0:0","${ddeb_tmp}/DEBIAN/control");
	}

	# Generate and install control file.
	doit("dpkg-gencontrol", "-p$package", "-l$changelog", "-T$substvars",
		"-P$tmp",@{$dh{U_PARAMS}});

	# This chmod is only necessary if the user sets the umask to
	# something odd.
	doit("chmod","0644","$tmp/DEBIAN/control");
	
	doit("chown","0:0","$tmp/DEBIAN/control");
}

=head1 SEE ALSO

L<debhelper(7)>

This program is a part of debhelper.

=head1 AUTHOR

Joey Hess <joeyh@debian.org>

=cut

# Local Variables:
# indent-tabs-mode: t
# tab-width: 4
# cperl-indent-level: 4
# End:
